# ------------------------------------------------------------------------------------------------------------
# Copyright (c) 2026 Gunivers
#
# This file is part of the Bookshelf project (https://github.com/mcbookshelf/bookshelf).
#
# This source code is subject to the terms of the Mozilla Public License, v. 2.0.
# If a copy of the MPL was not distributed with this file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# Conditions:
# - You may use this file in compliance with the MPL v2.0
# - Any modifications must be documented and disclosed under the same license
#
# For more details, refer to the MPL v2.0.
# ------------------------------------------------------------------------------------------------------------

# Input:
# Storage: bs:ctx _.template (a FSM)

# We list the states names in the collection output, this list will be useful to know if a transition refers to a state that does not exist
data modify storage bs:out collection.value set value []
data modify storage bs:out collection.value append from storage bs:ctx _.template.states[].name

# bs.collection works in bs:ctx _, so we move our context aside for the whole check
data modify storage bs:ctx fsm set from storage bs:ctx _
data modify storage bs:ctx fsm.states set value []
data modify storage bs:ctx fsm.states append from storage bs:ctx fsm.template.states[]

scoreboard players set #r bs.ctx 1

# We check if the FSM is valid

# First, we check if the FSM has an initial state
execute unless data storage bs:ctx fsm.template.initial run function #bs.log:error { \
  namespace: "bs.fsm", \
  path: "#bs.fsm:validate", \
  tag: "validate", \
  message: [{text: "The FSM does not have an initial state."}] \
}
execute unless data storage bs:ctx fsm.template.initial run scoreboard players set #r bs.ctx 0

# Then, we check if the FSM has at least one state
execute unless data storage bs:ctx fsm.states[0] run function #bs.log:error { \
  namespace: "bs.fsm", \
  path: "#bs.fsm:validate", \
  tag: "validate", \
  message: [{text: "The FSM does not have any state."}] \
}
execute unless data storage bs:ctx fsm.states[0] run scoreboard players set #r bs.ctx 0

# Finally, we have to check each state
execute unless score #r bs.ctx matches 0 store success score #r bs.ctx run function bs.fsm:check/internal/well_formedness_state

# We restore our context
data modify storage bs:ctx _ set from storage bs:ctx fsm
data remove storage bs:ctx fsm

# We return the result of the check
execute if score #r bs.ctx matches 0 run return fail
return 1
